# ============================================================================
# OH MY ZSH - Configuration de base
# ============================================================================

export ZSH="$HOME/.oh-my-zsh"
ZSH_THEME="robbyrussell"
plugins=(git)
source $ZSH/oh-my-zsh.sh

# ============================================================================
# CONFIGURATION
# ============================================================================

export PATH="$HOME/.local/bin:$PATH"
export ZSH_REPO="$HOME/zsh_config_repo"
export ZSHRC_WATCHER_FLAG="/tmp/.zshrc_watcher_running"
ZSHRC_FILES=(.zshrc .zshrc_arch .zshrc_debian)

# NVM
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"


# ============================================================================
# FONCTIONS
# ============================================================================

# Afficher les changements du .zshrc au reload
check_zsh_changes() {
    local current_file="${(%):-%x}"
    local cache="${current_file}.bak"
    if [[ -f "$cache" ]]; then
        diff --new-line-format=$'\e[32m+ Ligne %dn: %L\e[0m' \
             --old-line-format=$'\e[31m- Ligne %dn: %L\e[0m' \
             --unchanged-line-format="" \
             "$cache" "$current_file"
    fi
    cp "$current_file" "$cache"
}

# Synchroniser le .zshrc depuis GitHub (pull)
sync_zshrc() {
    if [ ! -d "$ZSH_REPO" ]; then
        echo "Erreur: Le dossier $ZSH_REPO n'existe pas."
        return 1
    fi
    (
        cd "$ZSH_REPO"
        if git remote | grep -q "origin"; then
            git pull --quiet origin main 2>/dev/null || git pull --quiet origin master 2>/dev/null || git pull --quiet
        fi
    )
    for file in $ZSHRC_FILES; do
        if [ -f "$ZSH_REPO/$file" ]; then
            if [ ! -f "$HOME/$file" ] || ! diff -q "$HOME/$file" "$ZSH_REPO/$file" >/dev/null 2>&1; then
                echo -e "\e[32m[Mise a jour du $file depuis le repo]\e[0m"
                cp "$ZSH_REPO/$file" "$HOME/$file"
            fi
        fi
    done
}

# Sauvegarder le .zshrc vers GitHub (push)
save_zshrc() {
    local msg="${1:-Auto update .zshrc}"
    if [ ! -d "$ZSH_REPO" ]; then
        echo "Creation du dossier $ZSH_REPO..."
        mkdir -p "$ZSH_REPO"
        (cd "$ZSH_REPO" && git init && echo "Repo initialise. Ajoutez votre remote origin !")
    fi
    for file in $ZSHRC_FILES; do
        if [ -f "$HOME/$file" ]; then
            cp "$HOME/$file" "$ZSH_REPO/$file"
        fi
    done
    (
        cd "$ZSH_REPO"
        git add -A
        if ! git diff --cached --quiet; then
            git commit -m "$msg" --quiet
            if git remote | grep -q "origin"; then
                git push --quiet origin master 2>/dev/null || git push --quiet origin main 2>/dev/null || git push --quiet
            fi
        fi
    )
}

# Git add, commit et push en une commande
gall() {
    git add .
    git commit -m "$@"
    git push
}

# Sauvegarder le .zshrc manuellement
gitz() {
    save_zshrc "$@"
}

# Pull le .zshrc depuis GitHub et recharge
pullz() {
    sync_zshrc && source ~/.zshrc
}

# Arreter le watcher manuellement
stopz() {
    if [ -f "$ZSHRC_WATCHER_FLAG" ]; then
        rm -f "$ZSHRC_WATCHER_FLAG"
        echo -e "\e[33m[Signal d arret envoye au watcher]\e[0m"
    else
        echo "Pas de watcher actif."
    fi
}

# Comparer recursivement deux dossiers avec un affichage clair
diffall() {
    local dir1="${1%/}" # Enleve le slash final si present
    local dir2="${2%/}"

    if [[ -z "$dir1" || -z "$dir2" ]]; then
        printf "\033[31mUsage: diffall <dossier1> <dossier2>\033[0m\n"
        return 1
    fi

    if [[ ! -d "$dir1" || ! -d "$dir2" ]]; then
        printf "\033[31mErreur: Les arguments doivent etre des dossiers valides.\033[0m\n"
        return 1
    fi

    printf "\033[34m=== Comparaison entre '%s' et '%s' (hors fichiers caches) ===\033[0m\n\n" "$dir1" "$dir2"

    # Liste unique, en ignorant les chemins contenant un point cache (*/.*)
    local all_files
    all_files=$( { 
        (cd "$dir1" && find . -type f -not -path '*/.*'); 
        (cd "$dir2" && find . -type f -not -path '*/.*'); 
    } | sort | uniq )

    if [[ -z "$all_files" ]]; then
        printf "\033[33mAucun fichier visible trouve.\033[0m\n"
        return 0
    fi

    while IFS= read -r file; do
        # Retirer le ./ au debut si present pour l'affichage
        local clean_name="${file#./}"
        local p1="$dir1/$file"
        local p2="$dir2/$file"

        # Cas 1: Manque dans Dossier 1
        if [[ ! -f "$p1" ]]; then
            printf "\033[33m[MANQUANT DANS %s]\033[37m %s\033[0m\n" "$dir1" "$clean_name"
            continue
        fi

        # Cas 2: Manque dans Dossier 2
        if [[ ! -f "$p2" ]]; then
            printf "\033[33m[MANQUANT DANS %s]\033[37m %s\033[0m\n" "$dir2" "$clean_name"
            continue
        fi

        # Cas 3: Comparaison du contenu
        if diff -q "$p1" "$p2" >/dev/null 2>&1; then
            printf "\033[32m[OK]\033[37m %s\033[0m\n" "$clean_name"
        else
            printf "\033[31m[DIFF]\033[37m %s\033[0m\n" "$clean_name"
            # Afficher les differences
            diff --color=always -u "$p1" "$p2" | tail -n +3 | sed 's/^/    /'
            echo ""
        fi

    done <<< "$all_files"
}

# Executer tous les scripts .py dans un dossier donne ou le dossier courant
pyall() {
    local dir="${1:-.}"
    if [[ ! -d "$dir" ]]; then
        printf "\e[31m[Erreur] %s n'est pas un dossier\e[0m\n" "$dir"
        return 1
    fi

    local found=0
    local rc
    while IFS= read -r -d '' f; do
        found=1
        printf "\e[34m[Exécution: %s]\e[0m\n" "$f"
        python3 "$f"
        rc=$?
        if [[ $rc -ne 0 ]]; then
            printf "\e[31m[Erreur (code %d) lors de %s]\e[0m\n" "$rc" "$f"
        else
            printf "\e[32m[Terminé: %s]\e[0m\n" "$f"
        fi
        printf "\n"
    done < <(find "$dir" -type f -name '*.py' -print0 2>/dev/null)

    if [[ $found -eq 0 ]]; then
        printf "\e[33m[Aucun fichier .py trouvé dans %s]\e[0m\n" "$dir"
        return 1
    fi
}

# Ouvrir le .zshrc (version Arch) avec surveillance
openz() {
    local target_file="$HOME/.zshrc_arch"
    touch "$ZSHRC_WATCHER_FLAG"
    TARGET_FILE="$target_file" gnome-terminal --title="ZSH Watcher" -- bash -c '
        SAVE_INTERVAL=120
        FLAG_FILE="/tmp/.zshrc_watcher_running"
        ZSH_REPO="$HOME/zsh_config_repo"
        
        echo -e "\e[34m========================================\e[0m"
        echo -e "\e[34m[Watcher actif - Surveille $TARGET_FILE]\e[0m"
        echo -e "\e[33mSauvegarde auto toutes les 2 min\e[0m"
        echo -e "\e[33mFerme VS Code pour arreter\e[0m"
        echo -e "\e[34m========================================\e[0m"
        echo ""
        
        save_and_push() {
            for file in ".zshrc" ".zshrc_arch" ".zshrc_debian"; do
                if [ -f "$HOME/$file" ]; then
                    cp "$HOME/$file" "$ZSH_REPO/$file"
                fi
            done
            cd "$ZSH_REPO"
            git add -A
            if ! git diff --cached --quiet; then
                git commit -m "Auto-save .zshrc" --quiet
                git push --quiet origin main 2>/dev/null || git push --quiet origin master 2>/dev/null || git push --quiet
                echo -e "\e[32m[$(date +%H:%M:%S)] Sauvegarde et push OK!\e[0m"
                return 0
            fi
            return 1
        }
        
        cleanup() {
            echo
            echo -e "\e[33m[Sauvegarde finale...]\e[0m"
            save_and_push
            rm -f "$FLAG_FILE"
            echo -e "\e[32m[Termine! Tape zs pour recharger]\e[0m"
            sleep 2
            exit
        }
        
        trap cleanup INT
        
        (codium -w "$TARGET_FILE"; rm -f "$FLAG_FILE") &
        
        last_hash=$(md5sum "$TARGET_FILE" 2>/dev/null | cut -d" " -f1)
        last_save=$(date +%s)
        
        while true; do
            sleep 2
            
            if [ ! -f "$FLAG_FILE" ]; then
                echo -e "\e[33m[VS Code ferme ou stopz]\e[0m"
                cleanup
            fi
            
            current_hash=$(md5sum "$TARGET_FILE" 2>/dev/null | cut -d" " -f1)
            now=$(date +%s)
            elapsed=$((now - last_save))
            
            if [[ "$current_hash" != "$last_hash" ]]; then
                last_hash="$current_hash"
                echo -e "\e[33m[$(date +%H:%M:%S)] Modification detectee\e[0m"
            fi
            
            if [ $elapsed -ge $SAVE_INTERVAL ]; then
                if save_and_push; then
                    last_save=$now
                else
                    echo -e "\e[90m[$(date +%H:%M:%S)] Pas de changement\e[0m"
                    last_save=$now
                fi
            fi
        done
    ' &> /dev/null &
}

# Ouvrir les 3 fichiers en même temps (base + arch + debian)
openza() {
    openz
    codium "$HOME/.zshrc" "$HOME/.zshrc_debian"
}

# ============================================================================
# ALIAS
# ============================================================================

# --- Norminette & Linting ---
alias norm="norminette -R CheckForbiddenSourceHeader"
alias normy='norminette 2>&1 | grep -v '\''OK'\'' | awk '\''NR==1{first=$0} NR>1{print} END{if(NR==1) print "\033[34m[\033[37mOK\033[31m]\033[0m"}'\'
alias flake='flake8 2>&1 | awk '\''{print} END{if (NR==0) print "\033[34m[\033[37mOK\033[31m]\033[0m"}'\'

# --- Compilation ---
alias c="cc -Wall -Wextra -Werror"

# --- Python ---
alias py="python3"
alias mp="mypy . --strict"
alias pa="pyall"
alias fm="flake && mp"

# --- Git ---
alias gp="git pull"

# --- ZSH Config ---
alias zs="source ~/.zshrc"
alias oz="openz"
alias opza="openza"

# --- Alias de con ---
alias rncp="echo -e '\033[0;33m ta gueule \033[0m'"
alias da="diffall"
# ============================================================================
# INITIALISATION (execute au chargement)
# ============================================================================

check_zsh_changes
echo -e "\033[30m[Arch aliases reloaded]\033[0m"
